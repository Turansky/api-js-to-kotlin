import static com.yworks.yfiles.api.generator.Generator.generateKotlinWrappers

group 'com.yworks.yfiles'
version '0.1-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.2.10'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin2js'

def defaultOutputDir = 'out'

clean.doFirst {
    delete 'src/main/kotlin'
    delete defaultOutputDir
}

// TODO: set depend on compileKotlin2Js
clean.doLast {
  // def source = project.properties["apiPath"] ?: throw GradleException("Invalid 'apiPath' parameter value!")
  def apiPath = "http://docs.yworks.com/yfileshtml/assets/api.04860ba1.js"
  generateKotlinWrappers(loadPath(apiPath), new File(projectDir, 'src/main/kotlin'))
}

compileKotlin2Js {
    kotlinOptions {
        outputFile = "$defaultOutputDir/test.js"
        moduleKind = 'amd'
        metaInfo = false
        sourceMap = false
    }
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'

    main.output.resourcesDir = defaultOutputDir
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
}

build.doLast {
    configurations.compile.each { File file ->
        copy {
            includeEmptyDirs = false

            from zipTree(file.absolutePath)
            into "${defaultOutputDir}/lib"
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith('.js') && !path.endsWith('.meta.js') && (path.startsWith('META-INF/resources/') || !path.startsWith('META-INF/'))
            }
        }
    }
}

def loadPath(path) {
  def start = "var apiData="
  return new URL(path).getText("UTF-8").substring(start.length())
}