import static com.yworks.yfiles.api.generator.Generator.generateKotlinWrappers

group 'com.yworks.yfiles'
version '2.1.0.2-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.2.40'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin2js'
apply plugin: 'maven-publish'

clean.doLast {
    delete 'out'
}

compileKotlin2Js {
    kotlinOptions {
        outputFile = "${projectDir}/out/yfiles-kotlin.js"
        moduleKind = 'amd'
        metaInfo = true
    }
}

task generateSources() {
    def apiPath = "https://docs.yworks.com/yfileshtml/assets/api.fdb0f7e0.js"
    generateKotlinWrappers(apiPath, new File(projectDir, 'src/main/kotlin'))
}

compileKotlin2Js.dependsOn generateSources

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
}

task mainJar(type: Jar) {
    from "$projectDir/out"
}

task sourceJar(type: Jar) {
    from "$projectDir/src/main/kotlin"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact mainJar

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}